import win.timer;

namespace aaz;
class async{
	ctor(){
		var requestIds = {};
		var timer;

		..table.gc(this, "close");
	};
	close = function(){
		if(timer){
			timer.disable();
			timer = null;
		}
	};
	await = function(id, info){
		requestIds[id] = {
			info = info;
			tick = ..time.tick();
			fiber = ..fiber.current();
		}
		return ..fiber.yield();
	};
	resume = function(id, ...){
		if(requestIds[id]){
			var f = requestIds[id].fiber
			if(..fiber.status(f) == ..fiber.status.dead){
				return ; 
			}
			
			// 最后的 yield  到  return 之间可能存在消息循环、队列， 从而运行检查超时的定时器函数，所以要先删除任务 id
			requestIds[id] = null;
			assert( ..fiber.resume(f, ...));
		}
	};
	run = function(func, onStart, onComplete, ...){
		var fib, err = ..fiber.create(
			function(...){
				if (onStart) onStart(...);
				var ret = {func(...)};		
				if (onComplete) onComplete(..table.unpackArgs(ret));
			}
		);
		assert(fib, err)
		assert(..fiber.resume(fib, ...));
	};
	startCheckTimeout = function(timeout, interval){
		this.timeout = timeout;
		this.checkTimeoutInterval = interval;
		
		timer = ..win.timer(null, this.checkTimeoutInterval);
		timer.onTimer = function(){
			if(!this.timeout){
				return ; 
			}
			var tick = ..time.tick();
			
			var removeIds = {}
			for(id, info in requestIds){
				if((tick - info.tick) > this.timeout){
					..table.push( removeIds, id )
				}
			}
			
			for(i=1;#removeIds;1){
				this.resume(removeIds[i], false, "timeout");
			}
			
		}
		timer.enable()
	}		
}
namespace async{
    
    
	
}
