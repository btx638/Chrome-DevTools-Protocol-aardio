//listener 监听器
import win.guid;

namespace aaz.libhpsocket;
class listener{
    ctor(componentClassPath){
		if(!componentClassPath) error("@参数1 componentClassPath不能为空", 2);
     	
        this._sessionId = tostring(..win.guid.create());
        // 避免监听器被自动回收
        ..table.push(__gcProtectTable__, this);
    };   
	createThreadCallback = function(callback, proto){
		if(!callback) error("@参数1 线程回调函数不能为空", 2);
		if(!proto) error("@参数2 线程回调函数原型不能为空", 2);
		
		return ..thread.tostdcall( 
			function(pSocket, ...){	
				if(!__componentClass__){
					global.import(owner.componentClassPath);
					__componentClass__ = eval(owner.componentClassPath);
					owner.componentClassPath = null;

					if(owner.threadGlobal){
						..table.mix(global, owner.threadGlobal);
						owner.threadGlobal = null;
					}
					
					if(owner.onThreadCreated){
						owner.onThreadCreated(owner.sessionId);
						owner.onThreadCreated = null;
						owner.sessionId = null;
					}
				}
				return owner.callback(__componentClass__(null, pSocket), ...);
			},
			proto,
			{
				callback = callback;
				componentClassPath = "aaz.libhpsocket." ++ componentClassPath;
				onThreadCreated = this.onThreadCreated;
				threadGlobal = this.threadGlobal;
				sessionId = this._sessionId;
			});	
	};
}
namespace listener{
    __gcProtectTable__ = {};	
}
