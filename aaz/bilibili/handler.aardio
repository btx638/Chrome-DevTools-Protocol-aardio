import web.json;
import util.metaProperty;
import aaz.libhpsocket.app.websocket.client.handler;
import aaz.bilibili.packet;

namespace aaz.bilibili.handler;
..table.mix(self, ..aaz.libhpsocket.app.websocket.client.handler)

afterUpgrade = function(extra){ 
	var roomId = extra.roomId.value;
	var token = extra.token.value; 
	
	return  ..aaz.bilibili.packet.getAuthPack(roomId, token)
}

// 因为是二进制传输，所以用 onBinMsg  
onBinMsg = function(extra){
	//  剩余数据的长度
	var unreadLen = extra.data.length;
	//  指针偏移，始终指向数据的头部
	var offset = 0;

	while(true){	
		// 消息头	
		var header = extra.data.convert(..aaz.bilibili.packet.header(), offset)
		// 标准消息长度
		var packetLen = ..raw.swap(header.packetLength, "int");

		// 不完整，存着下次再读
		if(unreadLen < packetLen){ 
			extra.data.toUnread(offset, unreadLen);
			extra.update()
			return ; 
		}
		
		// 心跳回复就直接结束
		var isHeartbeat = ..aaz.bilibili.packet.unpack(extra.data.pointer, offset, function(msgType, ret){
			..__postUi__(msgType, ret)
		});
		if(isHeartbeat){
			extra.data.new();
			extra.update()
			return ; 
		}

		
		// 偏移到下个数据
		offset += packetLen;
		
		unreadLen -= packetLen;
		// 读完了
		if(unreadLen == 0){
			extra.data.new();
			extra.update()
			return ; 
		}
	}	
}