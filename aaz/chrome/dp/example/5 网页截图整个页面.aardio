import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=759;bottom=469)
winform.add(
btnScreenshot={cls="button";text="截图";left=440;top=16;right=576;bottom=48;z=3};
edUrl={cls="edit";text="https://www.163.com/";left=72;top=16;right=424;bottom=48;edge=1;z=2};
static={cls="static";text="网址：";left=24;top=16;right=72;bottom=48;transparent=1;z=1}
)
/*}}*/

// https://dschnurr.medium.com/using-headless-chrome-as-an-automated-screenshot-tool-4b07dffba79a
// https://dev.to/gdledsan/selenium-4-and-chrome-driver-take-full-page-screenthos-2j8d
// https://github.com/puppeteer/puppeteer/blob/main/src/common/Page.ts#L2741

import console
import crypt.bin
import process
import aaz.chrome.dp;

var cdp, err = aaz.chrome.dp()

var task = function(url){
	cdp.open(null, true)
    cdp.connect();
	cdp.Page.enable();
	cdp.Page.navigate(
		url = url;
	)
	cdp.waitEvent( "Page.loadEventFired" );
	

/*
	cdp.Emulation.setDeviceMetricsOverride(
    	width =  1920;
    	height =  1080;
    	deviceScaleFactor = 0;
    	mobile = false;
    	fitWindow = false;
	)
	

	cdp.Emulation.setVisibleSize( 
		width = 1920;
	 	height = 1080
	 )
	 
	 cdp.Emulation.forceViewport(
		x = 0;
		y = 0;
		scale = 1;
	 );
*/
	 
	 
	
	var rect = cdp.Page.getLayoutMetrics()
	
	var ret = cdp.Page.captureScreenshot(
		format="jpeg";
		captureBeyondViewport = true;
		//fromSurface = true;

		clip = {
			x = 0;
			y = 0;
			scale = 1;
			width = rect.cssContentSize.width;
			height = rect.cssContentSize.height;
		}

	)
	if(ret){
		return crypt.bin.decodeBase64(ret.data); 	
	}
}

winform.btnScreenshot.oncommand = function(id,event){
	var url = winform.edUrl.text
	if(!string.startWith(url, "http")){
		winform.msgboxErr("请填写网址")
		return ; 
	}
	
	cdp.run(
		task,
		function(){
			winform.btnScreenshot.disabled = true
		},
		function(data){
			winform.btnScreenshot.disabled = false
			if(data){
				string.save("\截图.jpg", data)
				process.exploreSelect("\截图.jpg")
			}
		}, 
		url
	)
}


winform.show();
win.loopMessage();
return winform;