import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=385;bottom=140)
winform.add(
btnRunTask={cls="button";text="运行";left=142;top=43;right=221;bottom=83;z=1}
)
/*}}*/

io.open()

import aaz.chrome.dp;

var cdp, err = aaz.chrome.dp()
if(!cdp){
    winform.msgboxErr(err);
	return ; 
}

// 接收事件
cdp.on("Fetch.requestPaused", function(params){
	// 请求类型为图片就中止请求
	if(params.resourceType == "Image"){
		cdp.Fetch.failRequest(
			requestId = params.requestId;
			errorReason = "Aborted";
		);
	}
	else {
		// 继续其他的请求
		cdp.Fetch.continueRequest(
			requestId = params.requestId;
		);
	}
})
	
// 任务函数
var task = function(){
 	// 打开浏览器
	var ok, err = cdp.open()
    if(!ok){
         io.print("打开浏览器失败", err);
    	return ; 
    }
	
	// 连接浏览器
    var ok, err = cdp.connect();
    if(!ok){
        io.print("连接浏览器失败", err);
    	return ; 
    }
    

    // 订阅 Fetch 事件，用于对网络请求的拦截
	 cdp.Fetch.enable()
 
	// 订阅 Page 事件	
	var ok, err = cdp.Page.enable();
    if(!ok){
        io.print("订阅 Page 事件失败", err);
    	return ; 
    }
	
	// 打开网址
	var ok, err = cdp.Page.navigate(
		url = "https://www.cnbeta.com/";
	)
    if(!ok){
        io.print("打开网址失败", err)
    	return ; 
    }
	
	// 等待页面加载完成
	var ok, err = cdp.waitEvent( "Page.loadEventFired" );
    if(!ok){
        io.print("等待页面加载完成失败", err);
    	return ; 
    }
}

cdp.onTaskBegin = function(){
    winform.btnRunTask.disabled = true
}

cdp.onTaskEnd = function(...){
    winform.btnRunTask.disabled = false
}

winform.btnRunTask.oncommand = function(id,event){
	io.print(cdp.run(task));
}

winform.show();
win.loopMessage();