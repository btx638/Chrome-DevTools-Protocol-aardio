import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=663;bottom=383)
winform.add(
button={cls="button";text="运行";left=552;top=320;right=650;bottom=348;z=1};
edSelector={cls="edit";text=".items-area .item dl dt a";left=72;top=320;right=416;bottom=348;edge=1;z=6};
edUrl={cls="edit";text="https://www.cnbeta.com/";left=72;top=280;right=416;bottom=308;edge=1;z=4};
lvResult={cls="listview";left=9;top=7;right=652;bottom=271;edge=1;fullRow=1;z=2};
static={cls="static";text="url";left=11;top=286;right=64;bottom=310;align="right";transparent=1;z=3};
static2={cls="static";text="selector ";left=8;top=320;right=63;bottom=344;align="right";transparent=1;z=5}
)
/*}}*/

import win.ui.statusbar;
import aaz.chrome.dp;

var cdp, err = aaz.chrome.dp()
if(!cdp){
    winform.msgboxErr(err);
	return ; 
}
cdp.timeout = 9000;

var statusbar = win.ui.statusbar(winform);
statusbar.addItem("运行状态：", 70);
statusbar.addItem("就绪", -1);

winform.lvResult.insertColumn("序号",50);
winform.lvResult.insertColumn("标题",-1);

var uiLog = function(str){
	statusbar.setText(str, 2);
}

var currentRunningName;
var uiLogRunning = function(name){
    var str = string.format("正在%s...", name);
    currentRunningName = name;
	uiLog(str);	
}

var uiLogFail = function(name, err){
    name := currentRunningName;
    var str = string.format("%s失败，原因：%s", name, err);
	uiLog(str);
}

var uiResult = function(str){
	winform.lvResult.addItem({
		tostring(winform.lvResult.count+1);
		str
	});
}

var uiInit = function(){
	uiLog("任务开始", 2);
	winform.lvResult.clear();
}

var closeChrome = function(){
	return cdp.Browser.close(); 
}

var onTaskBegin = function(){
	winform.button.disabled = true
	currentRunningName = null;
	uiInit();
}

var onTaskEnd = function(result, duration){
	winform.button.disabled = false
	
	if(!duration){
		return ; 
	}
	uiLog("任务完成，用时：" ++ duration ++ "毫秒")
	
	// 读取结果
	for(i=1;#result;1){
		uiResult(result[i]);
	}
}

var doFail = function(name, err){
    uiLogFail(name, err);
	closeChrome();
	onTaskEnd();
	return false; 
}

var task = function(url, selector){
    onTaskBegin();
    var tick = time.tick()

 	uiLogRunning("打开浏览器");
	var ok, err = cdp.open(true);
    if(!ok){
        doFail( null, err);
    	return ; 
    }
	
	uiLogRunning("连接浏览器");
    var ok, err = cdp.connect();
    if(!ok){
        doFail( null, err);
    	return ; 
    }
    
	uiLogRunning("订阅 Page 事件");	
	var ok, err = cdp.Page.enable();
    if(!ok){
        doFail( null, err);
    	return ; 
    }
	
	// 打开网址
	uiLogRunning("打开网址");	
	var ok, err = cdp.Page.navigate(
		url = url;
	)
    if(!ok){
        doFail( null, err);
    	return ; 
    }
	
	uiLogRunning("等待页面加载完成");
	var ok, err = cdp.waitEvent("Page.loadEventFired");
    if(!ok){
        doFail( null, err);
    	return ; 
    }
 	
	uiLogRunning("开启 runtime");
	var ok, err = cdp.Runtime.enable()
	if(!ok){
		doFail( null, err);
		return ; 
	}
	
	uiLogRunning("编译脚本");
	var ret, err = cdp.Runtime.compileScript(
		sourceURL = url;
		persistScript = true;
		expression = /**
		 var getInnerText = function(selector){
       		var doms = document.querySelectorAll(selector);
       		var ret = new Array();
       		for(var i=0;i<doms.length;i++){
           		ret.push(doms[i].innerText);
       		}
       		return ret; 
		 }
		**/
	)

	if(!ret){
        doFail( null, err);
    	return ; 
	}
	// 检查编译的结果
	// 发现错误
	if(ret.exceptionDetails){
        doFail(
        	null, 
        	" 行号：" ++ ret.exceptionDetails.lineNumber ++
        	" 列号：" ++ ret.exceptionDetails.columnNumber ++ 
        	" 内容：" ++ ret.exceptionDetails.exception.description
        );
    	return ; 
	}
	
	
	uiLogRunning("运行脚本");
	var ret, err = cdp.Runtime.runScript(
		scriptId = 	ret.scriptId;
		returnByValue = true;
	)
	if(!ret){
		doFail(null, err);
		return ; 
	}
	
	uiLogRunning("对全局对象的表达式求值");
	var ret, err = cdp.Runtime.evaluate(
		expression = `getInnerText("` ++ selector ++ `")`;
		returnByValue = true;
	)
	if(!ret){
		doFail(null, err);
		return ; 
	}
	var result = ret.result;
	
	// 检测结果的类型
	// 看看有没有错误
	select(result.subtype) {
		case "error" {
			doFail(null, result.description);
			return ; 
		}
	}

	uiLogRunning("关闭浏览器");
	var ok, err = closeChrome();
	if(!ok){
		doFail(null, err);
		return ; 
	}

	onTaskEnd(result.value, time.tick() - tick);
	return true; 
}

winform.button.oncommand = function(id,event){
	cdp.run(task, winform.edUrl.text, winform.edSelector.text);
}

winform.show();
win.loopMessage();
return winform;